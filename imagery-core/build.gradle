import com.chrynan.imagery.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"
apply plugin: "com.chrynan.target.plugin"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        android()
        jvm()
        js(LEGACY) {
            browser()
            nodejs()
        }
        ios()
    }
    sourceSets {
        all {
            languageSettings {
                languageSettings.enableLanguageFeature("InlineClasses")
            }
        }
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                // Kotlinx Serialization
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1"

                // Kotlinx Coroutines
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"

                // Colors
                implementation "com.chrynan.colors:colors-core:0.3.0"
            }
        }
    }
}

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        minSdkVersion LibraryConstants.Android.minSdkVersion
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

// Android Main Dependencies
dependencies {
    implementation "androidx.exifinterface:exifinterface:1.3.2"
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://repo.repsy.io/mvn/chrynan/public"

                credentials {
                    username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                    password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId LibraryConstants.group
                artifactId "imagery-android"
                version LibraryConstants.versionName
            }
        }
    }
}

// Target Information
kotlinTargetDataGenerator {
    generateJson = true
    generateSvg = true
    outputPath = "${projectDir.path}/targets"
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
